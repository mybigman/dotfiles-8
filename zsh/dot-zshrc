# History
HISTFILE="$HOME/.zsh_history"
HISTSIZE=1200
SAVEHIST=1000
setopt hist_expire_dups_first
setopt hist_find_no_dups
setopt hist_ignore_dups
setopt hist_reduce_blanks

# Run pfetch
pfetch

# Prompt
autoload -Uz vcs_info
precmd() {
	vcs_info
	zstyle ':vcs_info:*' stagedstr '+'
	zstyle ':vcs_info:*' unstagedstr '!'
	zstyle ':vcs_info:*' check-for-changes true
	zstyle ':vcs_info:*' formats '%F{cyan} %b %c%u%m%f '
	zstyle ':vcs_info:git*+set-message:*' hooks git-untracked git-upstream
	zstyle ':vcs_info:*' enable git
	function +vi-git-untracked() {
		if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == "true" ]] && \
			[[ $(git ls-files --exclude-standard --others 2> /dev/null | wc -l) > 0 ]]; then
			hook_com[unstaged]+="?"
		fi
	}
	function +vi-git-upstream() {
		if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == "true" ]]; then
			local ahead="$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)"
			local behind="$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)"
			local text=""
			[[ "$ahead" > 0 ]] && text+="↥"
			[[ "$behind" > 0 ]] && text+="↧"
			hook_com[misc]+="$text"
		fi
	}
	PROMPT="%B%F{blue}%~%f ${vcs_info_msg_0_}%(?.%F{green}.%F{red})%(!.#.$)%f%b "
}

# Vi mode
bindkey -v
bindkey "^?" backward-delete-char
export KEYTIMEOUT=1
zle-keymap-select () { [ "$KEYMAP" = "vicmd" ] && printf "\033[2 q" || printf "\033[6 q" }
zle -N zle-keymap-select
zle-line-init () { zle -K viins; printf "\033[6 q" }
zle -N zle-line-init

# Autocompletion
autoload -Uz compinit && compinit
_comp_options+=(globdots)
zstyle ':completion::complete:*' gain-privileges 1
zstyle ':completion:*' menu select
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

# Syntax highlighting
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# Aliases
alias doas="doas "
alias v="$EDITOR"
alias rv="doas $EDITOR"
alias grep="grep --color=auto"
alias ls="ls --color=auto --human-readable --group-directories-first"
alias l="ls -Al"
alias ll="ls -l"
alias la="ls -a"
alias lla="ls -al"
alias ga="git add"
alias gaa="git add --all"
alias gah="git add --patch"
alias gb="git branch"
alias gc="git commit"
alias gcm="git commit -m"
alias gd="git diff"
alias gds="git diff --staged"
alias gpl="git pull"
alias gps="git push"
alias gs="git status"
alias xi="doas xbps-install"
alias xr="doas xbps-remove"
alias xq="doas xbps-query"

# Functions
function fv() {
	file="$(find "${1:-.}" -type f -printf "%P\n" | fzf)"
	[ "$file" ] && "$EDITOR" "${1:-.}/$file"
}
function fcd() {
	dir="$(find "${1:-.}" -type d -printf "%P\n" | fzf)"
	[ "$dir" ] && cd "${1:-.}/$dir"
}
